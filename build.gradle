import org.ajoberstar.gradle.git.tasks.GitLog

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:0.8'
        classpath 'org.ajoberstar:gradle-git:0.6.1'
    }

}

defaultTasks "clean", "build"

allprojects{
    apply plugin: 'wrapper'
    apply plugin: 'eclipse'

    group = "com.drsample.hello"
}

subprojects{
    apply plugin: 'java'
//    apply plugin: 'jacoco'
    apply from: file("$rootDir/version.gradle")

    repositories{
        mavenCentral()
    }


    jar {
        manifest {
            attributes 'Implementation-Title': "Dropwizard Sample ${project.name}",
                    'Implementation-Version' : "${project.version}",
                    'Built-By' : System.getProperty('user.name'),
                    'Built-JDK' : System.getProperty('java.version')
        }
    }

    task findGitVersion(type: GitLog){
        repoPath = "$rootDir"
        maxCommits = 1
        doLast{
            log.each {
                def gitSHA = it.id
                def shaAbbr = it.abbreviatedId
                jar{
                    manifest{
                        attributes 'Git-Commit' : gitSHA, 'Git-Commit-Abbr': shaAbbr
                    }
                }
            }
        }
    }

    jar.dependsOn findGitVersion

}
